# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.config/zsh/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# zsh-unplugged
function plugin-load {
  local repo plugdir initfile initfiles=()
  : ${ZPLUGINDIR:=${ZDOTDIR:-~/.config/zsh}/plugins}
  for repo in $@; do
    plugdir=$ZPLUGINDIR/${repo:t}
    initfile=$plugdir/${repo:t}.plugin.zsh
    if [[ ! -d $plugdir ]]; then
      echo "Cloning $repo..."
      git clone -q --depth 1 --recursive --shallow-submodules \
        https://github.com/$repo $plugdir
    fi
    if [[ ! -e $initfile ]]; then
      initfiles=($plugdir/*.{plugin.zsh,zsh-theme,zsh,sh}(N))
      (( $#initfiles )) || { echo >&2 "No init file '$repo'." && continue }
      ln -sf $initfiles[1] $initfile
    fi
    fpath+=$plugdir
    (( $+functions[zsh-defer] )) && zsh-defer . $initfile || . $initfile
  done
}

function completion-load {
    local completion name command completionfile
    for completion in $@; do
        name=${completion%% *}
        command=${completion#* }
        completionfile=$COMPLETIONSDIR/_$name
        if [[ ! -f $completionfile ]]; then
            echo "Creating completion file for $name..."
            # Capture the command output and status
            if output=$(eval "$command" 2>&1); then
                echo "$output" > "$completionfile"
            else
                echo "Failed to create completion for $name. Error: $output"
            fi
        fi
    done
}

ZPLUGINDIR="$ZDOTDIR/plugins"
plugins=(
    # Core
    romkatv/powerlevel10k
    zsh-users/zsh-completions
    zsh-users/zsh-autosuggestions
    Aloxaf/fzf-tab

    # Plugins
    MohamedElashri/eza-zsh
    Freed-Wu/zsh-help
    reegnz/jq-zsh-plugin

    # Completions
    eastokes/aws-plugin-zsh
    MenkeTechnologies/zsh-cargo-completion
    conda-incubator/conda-zsh-completion

    # Must be after everything else
    zdharma-continuum/fast-syntax-highlighting
    zsh-users/zsh-history-substring-search
)
plugin-load $plugins

# Setup extra completions
COMPLETIONSDIR=$ZDOTDIR/completions
[[ -d $COMPLETIONSDIR ]] || mkdir $COMPLETIONSDIR
fpath=($COMPLETIONSDIR \\$fpath)

# Format: "name command"
completions=(
    "docker docker completion zsh"
    "chezmoi chezmoi completion zsh"
    "volta volta completions zsh"
    "pnpm pnpm completion zsh"
    "yq yq completion zsh"
)
completion-load $completions

# Load completions
# setopt interactive_comments # needed for zsh-autocomplete
autoload -U compinit && compinit

# To customize prompt, run `p10k configure` or edit $ZDOTDIR/.p10k.zsh.
[[ ! -f $ZDOTDIR/.p10k.zsh ]] || source $ZDOTDIR/.p10k.zsh

# Keybindings
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey '^j' jq-complete

# History
HISTSIZE=10000
HISTFILE=$ZDOTDIR/.zsh_history
SAVEHIST=$HISTSIZE
HISTDUP=erase

# Completion styling
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}' # casing doesn't matter
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no # fail-safe
# preview directory's content with eza when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'eza -1 --color=always $realpath'

# Aliases
[ -f $ZDOTDIR/.aliasrc ] && source $ZDOTDIR/.aliasrc

# Options
[ -f $ZDOTDIR/.optionrc ] && source $ZDOTDIR/.optionrc

# Shell integrations
source <(fzf --zsh)
eval "$(zoxide init --cmd cd zsh)"
eval "$(conda shell.zsh hook)"
